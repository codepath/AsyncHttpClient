apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group='com.codepath.libraries'

ext {
    ARTIFACT = "asynchttpclient"
    POM_NAME = "CodePath Async Http Client"
    POM_DESC = "Async Http Client wrapper using OkHttp"
    POM_SCM_URL = 'https://github.com/codepath/AsyncHttpClient'
    BASE_VERSION = 2
    VERSION_NAME = "2.0.1"
}

version = VERSION_NAME
archivesBaseName = ARTIFACT

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode BASE_VERSION
        versionName VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    api "com.squareup.okhttp3:okhttp:4.9.0"
    api 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                artifact androidSourcesJar

                // You can then customize attributes of the publication as shown below.
                groupId = group
                artifactId = ARTIFACT
                version = VERSION_NAME
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['release']

    pkg {
        repo = 'maven'
        name = 'asynchttpclient'
        userOrg = 'codepath'
        licenses = ['Apache-2.0']
        vcsUrl = POM_SCM_URL
        version {
            name = BASE_VERSION
            desc = POM_NAME
            released = new Date()
        }
    }

}
